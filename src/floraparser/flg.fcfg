%start Start

Start -> DESCRIPTION

CDELIM -> COMMA |

DESCRIPTION[subject=?s, chars=?clist] -> SUBJECT[H=?s] CDELIM CHARLIST[H=?clist]

SUBJECT[H=[orth=?o, mod=?m]] -> CBEGINNER AP[H=?m] N[category=structure, orth=?o]
SUBJECT[H=[orth=?o, mod=?m]] -> CBEGINNER AP[H=?m] N[category='life-form', orth=?o]
SUBJECT[H=[orth=?o, mod=?m]] -> CBEGINNER AP[H=?m] N[category='feature', orth=?o]
SUBJECT[H=[orth=?o]] -> CBEGINNER N[category=structure, orth=?o]
SUBJECT[H=[orth=?o]] -> CBEGINNER N[category=structure, orth=?o] OFP
SUBJECT[H=[orth=?o]] -> CBEGINNER N[category='life-form', orth=?o]
SUBJECT[H=[category=structure, orth=?o]] -> CBEGINNER UNK[orth=?o]

CHARLIST[H=(?h1+?h2)] -> CHAR[H=?h1] CHARLIST[H=?h2]
CHARLIST[] -> CHAR[+HD, H=?head]

CHAR[] -> CHRL[+HD, H=?head] CTERMINATOR
CHAR[H=[+withph]] -> WITHP[+HD, H=?head]
CHAR[category=presence] -> OR ABAR[category=presence, +HD, H=?head]
CHAR[category=presence] -> ABAR[category=presence, +HD, H=?head] OR

CHAR[H=[stage=?st]] -> STAGE[stage=?st] CHARLIST[+HD, H=?head]

CBEGINNER -> '¢'
CTERMINATOR -> COMMA | '$'
# CTERMINATOR ->

CHR[category=?c, H=[stage=?st]] ->  CHR[category=?c, +HD, H=?head] STAGE[stage=?st]
CHR[category=?c, H=[stage=?st]] ->  CHR[category=?c, +HD, H=?head] SUBSET[stage=?st]
CHR[category=?c, H=[posit=?p]] ->  CHR[category=?c, +HD, H=?head] POSITION[H=?p]
CHAR[category=clist, H=[posit=?p]] ->  POSITION[H=?p] CHARLIST[+HD, H=?head]

CHR[category=?c] ->  APL[category=?c, +HD, H=?head]
CHR[category=?c] ->  AP[category=?c, +HD, H=?head]
CHR[category=?c] ->  WITHP[category=?c, +HD, H=?head]

CHR[+anaph, category=?c] -> ANAPH[category=?c] CHARLIST[+HD, H=?head]

CHR[category=?c] -> ISA NPL[category=?c, +HD, H=?head]
ISA -> IS

CHR[category=size] ->  SIZEB[+HD, H=?head]
CHR[category=count] ->  COUNT[+HD, H=?head]
#CHR[category=group] -> P[prep='in'] NP[H=[+group], +HD]

CHRL[category=?c] ->  CHR[+HD, H=?head, category=?c]

NL[category={?c+?d}, H=[orth=(?o+?ol)]] -> N[category=?d, orth = ?o] NL[category=?c, orth=?ol, +HD, H=?head]
NL[category=?c, orth=?o, H=[orth=?o, category=?c], group=?g] -> N[category=?c, orth=?o, group=?g]

NBAR[category=?c, H=[mod=?m]] -> AP[H=?m] NL[category=?c, +HD, H=?head]
NBAR[category=?c, H=[count=?count]] -> COUNT[H=[val=?count]] NBAR[category=?c, +HD, H=?head]
NBAR[category=?c] -> NL[category=?c, +HD, H=?head]
NBAR[category=?c] -> NBAR[category=?c, +HD, H=?head] OFP
NBAR[category=?c, H=[mod=?prep]] -> NBAR[category=?c, +HD, H=?head] PP[H=?prep]

NBAR[category=?c] -> NBAR[category=?c, +HD, H=?head] PAREXP
NBAR[category=pro] -> PRO

#NP[category=?c] -> DET NBAR[category=?c, +HD, H=?head] | ART NBAR[category=?c, +HD, H=?head] | NBAR[category=?c, +HD, H=?head]
NP[category=?c] -> DET NBAR[category=?c, +HD, H=?head] | ART NBAR[category=?c, +HD, H=?head] | NBAR[category=?c, +HD, H=?head]

NP -> NAME
#NP -> A[+compar] NBAR THAN S
#NP -> A[+compar] NBAR THAN S/NBAR

NP -> ANAPH

NP[H=(?h1,'AND',?h2)] -> NP[H=?h1] AND NP[H=?h2]

NPL[H=(?h1+'OR'+?h2), category=?c] -> NP[H=?h1, category=?c] ORDELIM NPL[H=?h2, category=?c]
NPL[category=?c] -> NP[+HD, H=?head, category=?c]
#NPL[H=(?h1+'OR'+?h2), category=?c, +OR] -> NPL[H=?h1, category=?c] CDELIM OR NP[H=?h2, category=?c]

N[+group, category='structure-infl', orth = ?o] -> N[category='structure-infl', orth = ?o]

BYX -> CONJ[conj='×'] | 'x'
SIZE[category=size, H=[category=size, num=?num, unit=?unit, dim=?dim]]  -> NUM[orth=?num] UNIT[orth=?unit] DIMEN[dim=?dim]
SIZE[category=size, H=[category=size, num=?num, unit=?unit, dim=long]]  -> NUM[orth=?num] UNIT[orth=?unit]
SIZE[category=size, H=[category=size, length=?num1, width=?num2, unit=?unit]]  -> NUM[orth=?num1] BYX NUM[orth=?num2] UNIT[orth=?unit]

SIZEB[category=size, H=(?h1+?h2)] -> SIZE[H=?h1] AND SIZE[H=?h2]
SIZEB[category=size, H=(?h1+?h2)] -> SIZE[H=?h1] COMMA SIZE[H=?h2]
SIZEB[category=size, H=[qual=?deg]] -> DEG[orth=?deg] SIZE[H=?h1, +HD]
SIZEB[category=size] -> SIZE[H=?h1, +HD]  | RANGE SIZE[H=?h1, +HD]
SIZEB[category=size, H=?h1] -> SIZEB[H=?h1] OR A[+compar]

DIMEN[dim=?dim] -> P[prep='in'] DIM[orth=?dim]
DIMEN[dim=?dim] -> DIM[orth=?dim]
COUNT[category=count, H=[category=count, val=?num]] -> NUM[orth=?num]
COUNT[category=count, H=[category=count, +per, val=?num, obj=?h]] ->  NUM[orth=?num] P[prep='per'] NBAR[H=?h]
COUNT[category=count, H=[category=count, val=?num, group=?h]] -> NUM[orth=?num] N[+group, orth=?h]
COUNT[H=[qual=?deg]] -> DEG[orth=?deg] COUNT[+HD, H=?head]
COUNT[H=[qual=?deg]] -> RANGE[orth=?deg] COUNT[+HD, H=?head]

ADVB[H=[orth=?o]] -> ADV[orth=?o]
ADVB[H=[qual=?o]] -> DEG[orth=?o]

#ADVB[H=[mod=?m]] -> DEG[orth=?m] ADVB[H=?o, +HD]
ADVB[H=[TO=(?h1,?h2)]] -> ADVB[H=?h1] TO ADVB[H=?h2]
ADVB[H=[mod=?o2]] -> ADVB[H=?o1] ADVB[H=?o2, +HD]
ADVB[H=[TO=(?o1,?o2)]] -> ADVB[H=?o1] TO ADVB[H=?o2]
ADVB[H=[OR=(?o1,?o2)]] -> ADVB[H=?o1] OR ADVB[H=?o2]
DEG -> COMPADJ DEG
ADVB[+comp, category=adv, orth=?o] -> A[+makecomp] ADV[orth=?o]

MOD[orth=?adv] -> ADVB[orth=?adv] |  A[orth=?adv]
#MOD[orth=?adv] ->  DEG[orth=?deg]  COUNT[H=?adv]
#ABAR[category=?c, H=[orth=?o, category=?c]em] -> A[category=?c, orth=?o, -positionem]
ABAR[category=?c, H=[orth=?o, category=?c, mod=?adv]] -> MOD[orth=?adv] A[category=?c, orth=?o, -position]
ABAR[category=?c, H=[orth=?o, category=?c, qual=?deg]] -> DEG[orth=?deg] A[category=?c, orth=?o, -position]
ABAR[category=?c, H=[orth=?o, category=?c]] ->  A[category=?c, orth=?o, -position]
ABAR[category=?c, H=[orth=(?a1+?a2), category=?c]] -> A[orth=?a1] A[category=?c, orth=?a2, -position]
ABAR[category=?c, H=[orth=?o, category=?c, mod=?adv]] -> COUNT[H=?adv] A[category=?c, orth=?o, -position]

ORDELIM -> COMMA | OR | COMMA OR
ABARL[category=?c] ->  ABAR[category=?c, H=?h1] ORDELIM[H=[category=?c, OR=(?h1+?h2)], +HD] ABARL[category=?c, H=[OR=?h2], -position]
ABARL[category=?c, H=[OR=?h]] ->  ABAR[category=?c, H=?h, +HD, -position]

ABAR[category=?c, H=[category=?c, orth=(?o1+'OR'+?h2)]] -> ABAR[category=?c, H=[orth=?o1]] OR WITHP[H=?h2]
ABAR[category=?c] -> ABAR[category=?c, H=?h1] TO[H=[category=?c, TO=(?h1+?h2)], +HD] ABAR[category=?c, H=?h2]

#ABAR[category=?c, H=[category=?c, orth=?o1, position=?p]] -> ABAR[category=?c, H=[orth=?o1]] POSITION[posit=?p]

A[category=UNK, orth=?o] -> UNK[category=?c, orth=?o]

ABAR[category=?c, H=?p, mod=?m] -> DET[orth=?m] PP[category=?c, H=?p, +HD, mod=?m]
ABAR[category=?c, H=?p, mod=?m] -> PP[category=?c, +HD, H=?p, pmod=?m]

#ABAR[H=[compto=?h, comp=?o], +compar] -> A[+compar, orth=?o] THAN NP[+HD, H=?head]

ABAR -> NUM TIMES A[+compar] NP
ABAR[category=?c] -> ABAR[category=?c, +HD, H=?head] PAREXP

AP[category=?c] -> ABARL[category=?c, +HD, H=?head]

# AP[category=?c, H=?h] -> AP[category=?c, H=?h] PAREXP
# AP[category=(?c+?d), H=(?ha+'AND'+?hb)] -> AP[category=?c, H=?ha] AND AP[category=?d, H=?hb]

WITHP[category=?c, H=[+withph]] -> WITH NP[category=?c, +HD, H=?head]
WITHP[category=?c, H=[+withph, clist=?cl]] -> WITH NP[category=?c, +HD, H=?head] CHARLIST[H=?cl]
WITHP[category=?c, H=[deg=?m]] -> DEG[orth=?m] WITHP[category=?c, +HD, H=?head]
# WITHP[category=?c, H=[+presence, obj=?h, clist=?cl]] -> NP[category=?c, H=?h] CHRL[H=?cl]

WITHP[category=?c] -> WITH NP[category=?c, H=?h1] AND[H=[AND=(?h1+?h2)], +HD]  NP[category=?c, H=?h2]

OFP[category=?c, H=[prep=OF]] -> OF NP[category=?c, +HD, H=?head]

PP[H=[prep=?prep, obj=?n]] -> P[orth=?prep, -position] NP[H=?n]
PP[H=[qual=?deg]] -> DEG[orth=?deg] PP[+HD, H=?head]

P[orth=?p] -> A[category=fusion, orth=?p] TO
P[orth=?p] -> A[category=fusion, orth=?p] WITH

PAREXP -> PUNC[punc='('] CHRL PUNC[punc=')']
#PAREXP -> COMMA AP COMMA | PPAREXP

AND -> CONJ[conj=and]
OR  -> CONJ[conj=or]
CONJUNCTION -> CONJ[conj='and'] | CONJ[conj='but']

STAGE[stage=?o] -> P[prep='in'] A[category='life-stage', orth=?o]
STAGE[stage=?o] -> P[prep='in'] A[category='life-stage', orth=?o] 'state'
STAGE[stage=?o] -> 'at' A[category='life-stage', orth=?o]
STAGE[stage=?o] -> A[+timing, orth=?o]

POSITION[H=[posit=?o]] -> P[prep=?p, orth=?o, +position]
POSITION[H=[posit=?prep, obj=?n]] -> P[prep=?prep, +position] NP[H=?n]
POSITION[H=[posit=?o]] -> A[+position, orth=?o]

SUBSET[stage=(?a+?n)] -> P[prep='in'] ABARL[orth=?a] N[orth=?n]

ANAPH[category=?o] -> ART A[orth=?o] 'ones'
ANAPH[category=?o] -> ART A[orth=?o] 'one'


